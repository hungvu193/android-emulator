apply plugin: 'com.android.application'


def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('sign.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}
android {
    compileSdkVersion 29
    flavorDimensions "default"


    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }

    }

    defaultConfig {
        applicationId "com.nostalgiaemulators.nes"
        minSdkVersion 15
        targetSdkVersion 29
        versionCode 111
        versionName "2.0.9"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                targets "nostalgia"
                arguments "-DCMAKE_BUILD_TYPE=Release" // aby i debug buildy byly rychly
            }
        }
    }



    splits {
        abi {
            enable true
            reset()
            include  'armeabi-v7a', 'arm64-v8a' //select ABIs to build APKs for
            universalApk true //generate an additional APK that contains all the ABIs

        }
    }


    productFlavors {
        lite {
            resValue "string", "title_activity_main", "Nostalgia.NES Lite"
            resValue "string", "app_name", "Nostalgia.NES Lite"

            applicationId "com.nostalgiaemulators.neslite"
            resValue "string", "flavour_package", "com.nostalgiaemulators.neslite"

            //signingConfig signingConfigs.debug
        }

        full {
            resValue "string", "title_activity_main", "Nostalgia.NES Pro"
            resValue "string", "app_name", "Nostalgia.NES Pro"

            applicationId "com.nostalgiaemulators.nesfull"
            resValue "string", "flavour_package", "com.nostalgiaemulators.nesfull"

            //signingConfig signingConfigs.debug
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
    }
    externalNativeBuild {
        cmake {
            path file('src/main/jni/CMakeLists.txt')
        }
    }


}



dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
    implementation project(':core')
}
